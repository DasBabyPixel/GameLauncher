ext {
    jansi = 'org.fusesource.jansi:jansi:2.4.0'
    fastutil = 'it.unimi.dsi:fastutil:8.5.8'
    guava = 'com.google.guava:guava:31.1-jre'
    property = 'de.dasbabypixel.api:property:1.0.3'
    joml = 'org.joml:joml:1.10.4'
    gson = 'com.google.code.gson:gson:2.9.0'
    pngdecoder = 'org.l33tlabs.twl:pngdecoder:1.0'
    lwjglVersion = '3.3.1'
    netty = 'io.netty:netty-all:4.1.79.Final'
}

tasks.register("generateRunDirectory") {
    doLast {
        mkdir "run"
    }
}

allprojects {
    group = 'de.dasbabypixel.gamelauncher'
    version = '0.0.1-SNAPSHOT'
    pluginManager.withPlugin('java') {
        java.toolchain.languageVersion.set(JavaLanguageVersion.of(11))
        pluginManager.withPlugin('maven-publish') {
            java {
                withSourcesJar()
                withJavadocJar()
            }
            publishing {
                publications {
                    maven(MavenPublication) {
                        artifactId project.name
                        groupId = project.group
                        version = project.version
                        from components.java
                    }
                }
                repositories {
                    maven {
                        name = 'DarkCube'
                        credentials PasswordCredentials
                        url = 'https://nexus.darkcube.eu/repository/dasbabypixel/'
                    }
                }
            }
        }
    }
}
