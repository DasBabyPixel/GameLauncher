plugins {
    id 'com.android.application' version '7.4.2' apply false
    id 'com.android.library' version '7.4.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.0' apply false
}

tasks.register("generateRunDirectory") {
    doLast {
        mkdir "run"
    }
}
generateRunDirectory

allprojects {
    group = 'de.dasbabypixel.gamelauncher'
    version = '0.0.1-SNAPSHOT'
    tasks.withType(JavaCompile).configureEach {
        targetCompatibility = JavaVersion.VERSION_11
        sourceCompatibility = JavaVersion.VERSION_11
    }
    pluginManager.withPlugin('java') {
        pluginManager.withPlugin('maven-publish') {
            java {
                withSourcesJar()
                withJavadocJar()
            }
            publishing {
                publications {
                    maven(MavenPublication) {
                        artifactId project.name
                        groupId = project.group
                        version = project.version
                        from components.java
                    }
                }
                repositories {
                    maven {
                        name = 'DarkCube'
                        credentials PasswordCredentials
                        url = 'https://nexus.darkcube.eu/repository/dasbabypixel/'
                    }
                }
            }
        }
    }
}
